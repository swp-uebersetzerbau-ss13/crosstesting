apply plugin: 'eclipse'

ant.importBuild 'javabite/build.xml'
defaultTasks ':crosstest:test'
project(':crosstest') {
	apply plugin: 'java'
	repositories {
		mavenCentral()
	}

	dependencies {
		compile 'log4j:log4j:1.2.17'
		compile 'org.apache.commons:commons-io:1.3.2'
		compile 'commons-lang:commons-lang:2.6'
		compile 'org.slf4j:slf4j-simple:1.7.5'
		compile 'org.ow2.asm:asm-all:4.1'
		testCompile 'junit:junit:4.11'
		testCompile 'junit-addons:junit-addons:1.4'
		testCompile 'com.e-movimento.tinytools:privilegedaccessor:1.2.1'
		testCompile 'org.hamcrest:hamcrest-core:1.3'
		compile project(':common')
		compile fucJarFiles()
		compile javabiteJarFiles()
	}
}
project(':common'){
	apply plugin: 'java'
	sourceSets {
		main {
			java { srcDir 'interfaces/src' }
		}
		test {
			java { srcDir 'interfaces/test' }
		}
	}
	repositories {
		mavenCentral()
	}

	dependencies {
		testCompile 'junit:junit:4.11'
		testCompile 'org.hamcrest:hamcrest-core:1.3'
	}
}
project(':fuc') {
	ant.importBuild 'code/build.xml'
}
task jbJars(dependsOn: ':buildForCrossTest')
task fucJars(dependsOn: ':fuc:dist')
task test(dependsOn: ':crosstest:test')
test.mustRunAfter jbJars
test.mustRunAfter fucJars
task buildAndTest(dependsOn: ['jbJars','fucJars','test'])

def fucJarFiles() {
	files('./fuc/code/dist/FUC-SymbolTable.jar','./fuc/code/dist/FUC-AST.jar',
		'./fuc/code/dist/FUC-Lexer.jar','./fuc/code/dist/FUC-Parser.jar',
		'./fuc/code/dist/FUC-Semantic-Analyser.jar','./fuc/code/dist/FUC-IRGen.jar',
		'./fuc/code/dist/FUC-Backend.jar','./fuc/code/dist/lib/log4j-1.2.17.jar')
}
def javabiteJarFiles() {
	files('./javabite/bin/javabite-semantic-0.2.jar','./javabite/bin/javabite-common-0.2.jar',
		'./javabite/bin/javabite-parser-0.2.jar','./javabite/bin/javabite-backend-0.2.jar',
		'./javabite/bin/javabite-lexer-0.2.jar','./javabite/bin/javabite-code-gen-0.2.jar',)
}
